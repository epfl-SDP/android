import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        mavenCentral()
        google()
        maven {
            name "githubJacoco"
            url "https://maven.pkg.github.com/epfl-SDP/jacoco-compose"
            credentials(PasswordCredentials)
        }
    }
}

rootProject.name = "SDP"

include ":mobile"

// Enable Gradle's version catalog support
// https://docs.gradle.org/current/userguide/platforms.html
enableFeaturePreview("VERSION_CATALOGS")

// Enable build cache on Cirrus CI.
ext.isCiServer = System.getenv().containsKey("CIRRUS_CI")
ext.isDefaultBranch = System.getenv()["CIRRUS_BRANCH"] == "main"
ext.buildCacheHost = System.getenv().getOrDefault("CIRRUS_HTTP_CACHE_HOST", "localhost:12321")

buildCache {
    local {
        enabled = !isCiServer
    }
    remote(HttpBuildCache) {
        url = "http://${buildCacheHost}/"
        enabled = isCiServer
        push = isDefaultBranch
    }
}
